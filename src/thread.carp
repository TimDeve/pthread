(load "./unsafe-ptr.carp")

(system-include "pthread.h")
(Project.config "cflag" "-pthread")

(use-all Result Array)

(register-type Thread "pthread_t")
(defmodule Thread
  (deftemplate copy (Fn [(Ref Thread)] Thread)
                    "pthread_t $NAME(pthread_t *ptr)"
                    "inline $DECL {return *ptr;}")
  (implements copy Thread.copy)

  (deftemplate init (Fn [] Thread)
                    "pthread_t $NAME()"
                    "inline $DECL {pthread_t thread = 0; return thread;}")

  (register create-c (Fn [(Ref Thread) (Ptr ()) (Ptr ()) (Ptr ())] Int) "pthread_create")

  (sig create-from-handle (Fn [(Ref Thread) (Ref (Fn [] ()))] (Result Int Int)))
  (defn create-from-handle [thread fun]
    (let [error-code (create-c thread
                               NULL
                               (UnsafePtr.fn-ptr fun)
                               (UnsafePtr.fn-env-ptr fun))]
      (if (= 0 error-code)
          (Success error-code)
          (Error error-code))))

  (sig create (Fn [(Ref (Fn [] ()))] (Result Thread Int)))
  (defn create [fun]
    (let [thread (init)]
      (map (create-from-handle &thread fun)
           &(fn [_] thread))))

  (register exit (Fn [(Ptr ())] ()) "pthread_exit")

  (register join-c (Fn [Thread (Ptr ())] Int) "pthread_join")
  (sig join (Fn [Thread] (Result Int Int)))
  (defn join [thread]
    (let [error-code (join-c thread NULL)]
      (if (= 0 error-code)
          (Success error-code)
          (Error error-code))))

  (sig join (Fn [Thread] (Array (Result Int Int))))
  (defn join-all [threads]
    (copy-map &(fn [thread] (Thread.join @thread)) &threads))

  (register cancel-c (Fn [Thread] Int) "pthread_cancel")
  (sig cancel (Fn [Thread] (Result Int Int)))
  (defn cancel [thread]
    (let [error-code (cancel-c thread)]
      (if (= 0 error-code)
          (Success error-code)
          (Error error-code)))))

